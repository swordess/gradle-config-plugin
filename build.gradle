buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'groovy'
    id 'org.standardout.versioneye' version '1.1.1'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.5'
}

group 'org.swordess'
version '1.0.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.2'
    compile 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
    compile 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
}

task sourceJar(type: Jar) {
    dependsOn project.tasks.getByName('classes')
    from sourceSets.main.allSource
    classifier 'sources'
    extension 'jar'
    group 'build'
}

publishing {
    publications {
        swordess(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['swordess']
    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/xingyuli/swordess-gradle-config-plugin.git'
        version {
            name = project.version
            released  = new Date()
        }
    }
}
